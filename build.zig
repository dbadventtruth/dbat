const std = @import("std");

fn getSourceFiles(b: *std.Build, base_path: []const u8, ext: []const u8) []const []const u8 {
    const allocator = b.allocator;
    const io = b.graph.io;
    const empty: []const []const u8 = &[_][]const u8{};

    var dir = std.Io.Dir.cwd().openDir(io, base_path, .{ .iterate = true }) catch return empty;

    var walker = dir.walk(allocator) catch {
        dir.close(io);
        return empty;
    };
    defer walker.deinit();

    var capacity: usize = 16;
    var files = allocator.alloc([]const u8, capacity) catch return empty;
    var count: usize = 0;

    while (true) {
        const next_opt = walker.next(io) catch continue;
        if (next_opt) |entry| {
            if (entry.kind == .file and std.mem.endsWith(u8, entry.path, ext)) {
                if (count >= capacity) {
                    capacity *= 2;
                    files = allocator.realloc(files, capacity) catch return empty;
                }
                // Prepend base_path to get full relative path
                files[count] = std.fmt.allocPrint(allocator, "{s}/{s}", .{ base_path, entry.path }) catch return empty;
                count += 1;
            }
        } else {
            break;
        }
    }

    dir.close(io);
    return files[0..count];
}

pub fn build(b: *std.Build) void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    // Dependencies
    const zlua_dep = b.dependency("zlua", .{
        .target = target,
        .optimize = optimize,
    });

    // C library - libs/db
    const mod_dbat_db = b.createModule(.{
        .target = target,
        .optimize = optimize,
        .link_libc = true,
        .link_libcpp = true,
        .root_source_file = b.path("libs/db/src/root.zig"),
    });

    mod_dbat_db.addIncludePath(b.path("libs/db/include"));
    const db_files = getSourceFiles(b, "libs/db/src", ".cpp");
    mod_dbat_db.addCSourceFiles(.{ .files = db_files, .flags = &[_][]const u8{ "-std=gnu++23", "-w", "-g" } });
    mod_dbat_db.addImport("zlua", zlua_dep.module("zlua"));

    const dbat_db = b.addLibrary(.{
        .name = "dbat_db",
        .linkage = .static,
        .root_module = mod_dbat_db,
    });

    const mod_dbat_game = b.createModule(.{
        .target = target,
        .optimize = optimize,
        .link_libc = true,
        .link_libcpp = true,
        .root_source_file = b.path("libs/game/src/root.zig"),
    });
    mod_dbat_game.addIncludePath(b.path("libs/db/include"));
    mod_dbat_game.addIncludePath(b.path("libs/game/include"));
    mod_dbat_game.addImport("zlua", zlua_dep.module("zlua"));
    const game_files = getSourceFiles(b, "libs/game/src", ".cpp");
    mod_dbat_game.addCSourceFiles(.{ .files = game_files, .flags = &[_][]const u8{ "-std=gnu++23", "-w", "-g", "-DPATH_MAX=4096" } });

    const dbat_game = b.addLibrary(.{
        .name = "dbat_game",
        .linkage = .static,
        .root_module = mod_dbat_game,
    });
    mod_dbat_game.linkLibrary(dbat_db);

    // Executable - pure C app (circle)
    const circle_mod = b.createModule(.{ .target = target, .optimize = optimize, .link_libc = true, .link_libcpp = true });
    circle_mod.addIncludePath(b.path("libs/db/include"));
    circle_mod.addIncludePath(b.path("libs/game/include"));
    circle_mod.addCSourceFiles(.{ .files = &[_][]const u8{"apps/server/src/main.cpp"}, .flags = &[_][]const u8{ "-std=gnu++23", "-w", "-g", "-DPATH_MAX=4096" } });
    const exe = b.addExecutable(.{
        .name = "dbat",
        .root_module = circle_mod,
    });
    circle_mod.linkLibrary(dbat_db);
    circle_mod.linkLibrary(dbat_game);

    b.installArtifact(dbat_db);
    b.installArtifact(dbat_game);
    b.installArtifact(exe);

    const run_step = b.step("run", "Run the app");
    const run_cmd = b.addRunArtifact(exe);
    run_step.dependOn(&run_cmd.step);
    run_cmd.step.dependOn(b.getInstallStep());

    if (b.args) |args| {
        run_cmd.addArgs(args);
    }
}
